pluginManagement {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	plugins {
		// https://github.com/radarsh/gradle-test-logger-plugin/blob/develop/CHANGELOG.md
		id 'com.adarshr.test-logger'                    version '3.2.0'
		// https://github.com/diffplug/blowdryer/blob/main/CHANGELOG.md
		id 'com.diffplug.blowdryer'                     version '1.7.0'
		id 'com.diffplug.blowdryerSetup'                version '1.7.0'
		// https://github.com/diffplug/spotless/blob/main/plugin-gradle/CHANGES.md
		// Stuck on 6.16.0 as the last version before Spotless adopted Solstice
		// Gradle tries to replace the Spotless' transitive dep with the solstice-in-progress
		id 'com.diffplug.spotless'                      version '6.16.0'
		// https://github.com/diffplug/spotless-changelog/blob/main/CHANGELOG.md
		id 'com.diffplug.spotless-changelog'            version '3.0.2'
		// https://plugins.gradle.org/plugin/com.gradle.plugin-publish
		id 'com.gradle.plugin-publish'                  version '1.2.0'
		// https://www.benediktritter.de/maven-plugin-development/#release-history
		id 'de.benediktritter.maven-plugin-development' version '0.4.2'
		// https://github.com/equodev/equo-ide/blob/main/plugin-gradle/CHANGELOG.md
		id 'dev.equo.p2deps'                            version '1.6.0'
		// https://github.com/gradle-nexus/publish-plugin/releases
		id 'io.github.gradle-nexus.publish-plugin'      version '1.3.0'
	}
}
plugins {
	id 'com.diffplug.blowdryerSetup'
	id 'com.adarshr.test-logger'                    apply false
	id 'com.diffplug.spotless'                      apply false
	id 'com.diffplug.spotless-changelog'            apply false
	id 'com.gradle.plugin-publish'                  apply false
	id 'de.benediktritter.maven-plugin-development' apply false
	id 'dev.equo.p2deps'                            apply false
	id 'io.github.gradle-nexus.publish-plugin'      apply false
}
blowdryerSetup {
	repoSubfolder ''
	devLocal 'gradle/blowdryer-scripts'
}
rootProject.name = 'equo-ide'
include 'solstice'
include 'patch-chromium-swt'
include 'patch-equinox-4.27'
include 'plugin-gradle'
include 'plugin-maven'
